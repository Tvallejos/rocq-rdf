From HB Require Import structures.
From mathcomp Require Import all_ssreflect.
Set Implicit Arguments.
Unset Strict Implicit.
Unset Printing Implicit Defensive.
Import Order.Theory.
Local Open Scope order_scope.

(* Lemmas complementing math-comp theories *)

Lemma all_undup (T : eqType) (s : seq T) p : all p (undup s) = all p s.
Proof.
  suffices /eq_all_r -> : (undup s) =i s by [].
  exact: mem_undup.
Qed.

Lemma inweak (T: eqType) (l:seq T) t u : t \in l -> t \in (u::l).
Proof. by rewrite -!has_pred1 /has; case (pred1 t u)=> [//| -> ]. Qed.

Lemma undup_in (T : eqType) (t : T) (s : seq T) :
  t \in s -> t \in undup s.
Proof. elim: s=> [//| h hs IHts].
       + rewrite /= in_cons. case/orP.
       - move=> /eqP<-; case e: (t \in hs) IHts; first by move=>->.
         by rewrite mem_head.
       - case e: (h \in hs); first by apply IHts.
         move=> /IHts tin_hs; apply: inweak tin_hs.
Qed.

Lemma undup_idem (T : eqType) (s : seq T) :
  undup (undup s) = undup s.
Proof.
  elim: s=> [//| t ts IHts] /=.
  by case e: (t \in ts); rewrite // /= mem_undup e IHts.
Qed.

Lemma map_undup_idem (T1 T2: eqType) (f : T1 -> T2) (s : seq T1):
  map f (undup (undup s)) = map f (undup s).
Proof. congr (map f); exact: undup_idem. Qed.

Lemma undup_cat_r (T: eqType) (s q : seq T) :
  undup (s ++ undup q) = undup (s ++ q).
Proof.
  elim: s=> [//| aq qs IHqs] /=; first exact: undup_idem.
  by rewrite !mem_cat mem_undup IHqs.
Qed.

Lemma undup_cat_l (T: eqType) (s q : seq T) :
  undup (undup s ++ q) = undup (s ++ q).
Proof.
  by rewrite !undup_cat undup_idem.
Qed.

Lemma mem_filter_map (T : eqType) f p a (s : seq T) :
  (forall b, p b = p (f b)) ->
  (a \in (map f (filter p s))) = (a \in filter p (map f s)).
Proof. elim: s=> [//|h t IHts] pres.
       by case e: (p h); rewrite /= e; rewrite pres in e; rewrite e /= ?in_cons IHts //.
Qed.

Lemma empty_permutations (T:eqType) : @permutations T [::] = [:: [::]]. Proof. by []. Qed.

Lemma map_inv (T U: eqType) (s:seq T) (f: T -> U):
  forall (u: U), u \in map f s -> exists t, u = (f t).
Proof. elim : s => [| a t IHts] u /=; first by rewrite in_nil.
       + rewrite in_cons; case/orP => [/eqP -> | y].
         by exists a.
              - by apply: IHts y.
Qed.

Lemma foldl_min (disp : Order.disp_t) (T: porderType disp) (l: seq T) (x0 : T) :
  foldl Order.min x0 l = x0 \/ foldl Order.min x0 l \in l.
Proof. elim: l x0 => [ | t ts IHts] x0; first by left.
       + rewrite in_cons /=; case: (IHts (Order.min x0 t))=> [ -> |intail] /=.
       - rewrite Order.POrderTheory.minEle; case: ifP=> _; first by left.
         * by right; rewrite eqxx.
       - by right; rewrite intail orbT.
Qed.

Lemma foldl_max (disp: Order.disp_t) (T: porderType disp) (l: seq T) (x0 : T) :
  foldl Order.max x0 l = x0 \/ foldl Order.max x0 l \in l.
Proof. elim: l x0 => [ | t ts IHts] x0; first by left.
       + rewrite in_cons /=; case: (IHts (Order.max x0 t))=> [ -> |intail] /=.
       - rewrite Order.POrderTheory.maxEle; case: ifP=> _; first by right; rewrite eqxx.
         * by left.
       - by right; rewrite intail orbT.
Qed.

Lemma perm_map_cancel (T1 T2: eqType) (s : seq T1) (f: T1 -> T2) (g: T2 -> T1) :
  cancel f g -> perm_eq (map (g \o f) s) s.
Proof. move=> can. elim: s=>[//| h t IHts] /=. by rewrite can perm_cons IHts. Qed.

Lemma perm_map_in_cancel (T: eqType) (s : seq T) (f g: T -> T) :
  {in s, cancel f g} -> perm_eq (map (g \o f) s) s.
Proof. elim: s=>[//| h t IHts] /=.
       move=> can. rewrite can. rewrite perm_cons IHts //.
       move=> x y. rewrite can //. by rewrite in_cons y orbT. by rewrite in_cons eqxx.
Qed.

Lemma perm_undup_map_inj (T1 T2: eqType) (f : T1 -> T2) s1 s2 :
  {in s1 &,injective f} ->
  uniq s1 ->
  perm_eq (undup (map f s1)) s2 ->
    perm_eq (map f s1) s2.
Proof. move=> injf ? peq.
       have equ: uniq (undup (map f s1)) = uniq (map f s1).
       by rewrite map_inj_in_uniq // undup_uniq.
       suffices eq : perm_eq (map f s1) (undup (map f s1)).
       by apply: perm_trans eq peq.
       apply uniq_perm.
       + by rewrite -equ undup_uniq.
       + by rewrite undup_uniq.
       + by move=> x; rewrite mem_undup.
Qed.

Open Scope order_scope.

Lemma max_foldlP:
  forall [disp : Order.disp_t] [T : orderType disp] (l : seq T) (x y : T),
    (foldl Order.max x l) = y -> (x <= y) && all (fun z=> z <= y) l.
Proof. move=> d T l x y.
       elim: l x=> [z /= -> //| hd t IHt]; first by rewrite Order.POrderTheory.lexx.
       move=> x. rewrite /= Order.POrderTheory.maxEle.
       case e: (x <= hd); move=> /IHt/andP[hdmax ->]; rewrite hdmax !andbT /=.
       + by apply (Order.POrderTheory.le_le_trans e hdmax (Order.POrderTheory.lexx _)).
       + rewrite Order.TotalTheory.leNgt /= Bool.negb_false_iff in e.
         by apply (Order.POrderTheory.le_le_trans (Order.POrderTheory.ltW e) hdmax (Order.POrderTheory.lexx _)).
Qed.

Lemma max_foldl_minimum:
  forall [disp : Order.disp_t] [T : porderType disp] (l : seq T) (x : T),
    (forall y, x <= y) -> foldl Order.max x l = x -> ((l == [::]) || (x \in l)).
Proof. move=> d T l x minimum.
       elim: l=> [//| hd t IHt].
       rewrite /= Order.POrderTheory.maxEle minimum.
       case: (foldl_max t hd).
       by move=> -> ->; rewrite in_cons eqxx.
       by move=> H <-; rewrite in_cons H orbT.
Qed.

Lemma sizeO_filter T (s : seq T) p: size (filter p s) == 0 = all (negb \o p) s.
Proof. by elim s=> //= h t <-; case (p h). Qed.

Definition build_finfun (T : choiceType) (f : T -> T) (s : seq T) : (seq_sub s) -> T :=
  fun ssub => f (ssval ssub).

Remark zip0s (S T : Type) (s:seq T) : zip (@nil S) s = [::]. by case s. Qed.
Remark zips0 (S T : Type) (s:seq S) : zip s (@nil T) = [::]. by case s. Qed.

Lemma in_zip (S T : eqType) (ss : seq S) (ts : seq T) s t:
  (s,t) \in zip ss ts -> (s \in ss) && (t \in ts).
Proof.
  elim : ts ss t s=> [|t' ts' IHts] /= ss t s; first by case: ss; rewrite in_nil.
  + case: ss=> [//|s' ss'].
    rewrite in_cons; case/orP; first by rewrite !in_cons xpair_eqE=> /andP [-> ->].
    by rewrite in_cons=> /IHts/andP [-> ->]; rewrite !Bool.orb_true_r.
Qed.

Lemma in_zip_sym (S T : eqType) (ss : seq S) (ts : seq T) s t:
  (s,t) \in zip ss ts = ((t, s) \in zip ts ss).
Proof.
  elim: ts ss=> [[//|//]| t' ts' IHts] ss.
  + case: ss=> [| s' ss']; first by rewrite zip0s.
  - rewrite /= !in_cons. f_equal; first by rewrite !xpair_eqE Bool.andb_comm.
    by rewrite IHts.
Qed.

Lemma notin_zip_l (S T: eqType) (ss: seq S) (ts : seq T) s t :
  s \notin ss -> (s,t) \notin zip ss ts.
Proof.
  rewrite /negb.
  by case e: (s \in ss); case e2: ((s, t) \in zip ss ts); move: e2=> // /in_zip; rewrite e.
Qed.

Lemma notin_zip_r (S T: eqType) (ss: seq S) (ts : seq T) s t :
  t \notin ts -> (s,t) \notin zip ss ts.
Proof.
  by rewrite in_zip_sym; apply notin_zip_l.
Qed.

Lemma zip_uniq_sym (S T : eqType) (ss : seq S) (ts : seq T) :
  uniq (zip ss ts) = uniq (zip ts ss).
Proof.
  elim: ts ss=> [[//|//]| t ts' IHts] ss.
  + case: ss => [| s ss' /=]; first by rewrite zip0s.
  - f_equal; last by apply IHts.
    by rewrite in_zip_sym.
Qed.

Lemma zip_uniq_l (S T : eqType) (ss : seq S) (ts : seq T) :
  uniq ss -> uniq (zip ss ts).
Proof.
  elim: ts ss=> [[//|//]| t' ts' IHts] ss uniq_ss.
  + case: ss uniq_ss => [| s ss]; first by rewrite zip0s.
    rewrite /==> /andP[nin_ss uniq_ss]; apply /andP; split; first by apply (notin_zip_l _ _ nin_ss).
    by apply: IHts uniq_ss.
Qed.

Lemma zip_uniq_r (S T : eqType) (ss : seq S) (ts : seq T) :
  uniq ts -> uniq (zip ss ts).
Proof.
  by rewrite zip_uniq_sym; apply zip_uniq_l.
Qed.

Lemma all_zip1 (T1 T2: Type) (s1: seq T1) (s2 : seq T2) p :
  all p s1 -> all (fun t=> p t.1) (zip s1 s2).
Proof. elim: s1 s2=> [|hd tl IHtl] s2; first by case s2.
       + move=> /andP[phd ptl] /=. case: s2=> //= b btl.
         by rewrite phd /= IHtl.
Qed.

Lemma all_zip_sym (T1 T2 : Type) (s1: seq T1) (s2 : seq T2) p :
  all (fun t=> p t.1) (zip s1 s2) = all (fun t => p t.2) (zip s2 s1).
Proof. by apply /idP/idP; elim: s1 s2=> [| x s IHs] [| y t]//= /andP[-> ts2] /=; rewrite IHs. Qed.

Lemma all_zip2 (T1 T2: Type) (s1: seq T1) (s2 : seq T2) p : all p s2 -> all (fun t=> p t.2) (zip s1 s2).
Proof. by move=> allp2; rewrite -all_zip_sym all_zip1. Qed.

Lemma uniq_zip_iota (T : eqType) (s: seq T) n m: uniq (zip s (iota n m)).
Proof. by apply: zip_uniq_r _ (iota_uniq _ _). Qed.

Lemma uniq_zip_map (T U V: eqType) (s: seq T) (u: seq U)
  (f : (T*U) -> V) (injF: injective f) : uniq (zip s u) -> uniq (map f (zip s u)).
Proof. by rewrite (map_inj_uniq injF). Qed.

Lemma in_all (T : eqType) (s : seq T) t f : t \in s -> all f s -> f t.
Proof. elim: s=> [| hd tl IHtl]; first by rewrite in_nil.
       by rewrite in_cons; case/orP=> [/eqP->/andP [//]| /IHtl _/andP[//]].
Qed.

Lemma size_0_nil (T : Type) (s : seq T) : size s = 0 -> s = [::].
Proof. by case s. Qed.

Lemma seq1_empty_seq (A : Type) (hd:A) d s : hd :: s = [:: d] -> s = [::].
Proof. by case s. Qed.

Definition map_fintype (T U: eqType) (s : seq T) (f : T -> U) (arg : seq_sub s) : seq_sub (map f s).
Proof. suffices: f (ssval arg) \in (map f s). by apply SeqSub.
       by apply (map_f f); apply (ssvalP arg).
Qed.

Lemma size_neq0 (T : eqType) (s : seq T): (size s != 0) = (s != [::]).
Proof. by congr negb; apply size_eq0. Qed.

Lemma permutations_neq_nil (T : eqType) (s : seq T) : permutations s != [::].
Proof. suffices: size (permutations s) != 0 by rewrite size_neq0.
       suffices : s \in permutations s.
         by case : (permutations s).
       by rewrite mem_permutations perm_refl.
Qed.


Fixpoint someT_to_T {T : Type} (os : seq (option T)) : seq T :=
  match os with
  | nil => nil
  | Some t :: oss => t :: someT_to_T oss
  | None :: oss => someT_to_T oss
  end.

Fixpoint app_n (T: Type)(f : T -> T) (x : T) (n:nat) :=
  match n with
  | O => x
  | S n' => f (app_n f x n')
  end.

Definition mapi {A B : Type} (f : A -> nat ->  B) (s : seq A) : seq B :=
  map (fun an => (f an.1) an.2) (zip s (iota 0 (size s))).

Definition fun_to_fin (T : eqType) (s : seq T) (f : T -> T) : seq_sub s -> T:=
  \val.
  (* fun s0=> let (ssval,_) := s0 in (f ssval). *)

Lemma eq_to_fin (T: eqType) (ft ft' : finType)
  (f : T -> T) (injF: injective f)
  (funt : ft -> T) (injFt: injective funt)
  (funt' : T -> ft') (injFt' : injective funt')
  : exists (f'' : ft -> ft'), injective f''.
Proof. exists (fun argT => (funt' (f (funt argT)))).
       by apply: (inj_comp injFt') (inj_comp injF injFt).
Qed.

Lemma max_sym disp (T: orderType disp) : symmetric Order.max.
Proof. by move=> x y; rewrite Order.POrderTheory.maxEle Order.POrderTheory.maxElt Order.TotalTheory.leNgt; case: (y < x)%O.
Qed.

Lemma min_sym : symmetric Order.min.
Proof. by move=> x y; rewrite Order.POrderTheory.minEle Order.POrderTheory.minElt Order.TotalTheory.leNgt; case: (y < x)%O.
Qed.

Lemma order_le_neq_antisym (disp : Order.disp_t) (T : orderType disp) (x y : T) : x != y -> (x <= y) == ~~ (y <= x).
Proof. rewrite neq_lt !leNgt=> /orP[] lxy; rewrite lxy -leNgt /=; apply /eqP.
       by apply ltW.
       rewrite leNgt negbK; apply/eqP; rewrite eq_sym; apply /eqP.
       by apply lt_gtF.
Qed.

Lemma has_not_default T (s : seq T) p :
  has p s -> forall x0 x1,  nth x0 s (find p s) = nth x1 s (find p s).
Proof. elim: s=> [//| hd tl IHtl] /= hps x1.
       by case/orP: hps=> [-> //| /IHtl hptl]; case: (p hd)=> //; apply hptl.
Qed.

Lemma perm_map_comp (T1 T2 T3 : eqType) (f: T1 -> T2) (g : T2 -> T3) s1 s2 s3 :
  perm_eq (map f s1) s2 ->
  perm_eq (map g s2) s3 ->
  perm_eq (map (g \o f) s1) s3.
Proof. by move=> /(perm_map g); rewrite -map_comp; apply perm_trans. Qed.

Lemma can_in_pcan_in (T1 T2 : eqType) (f : T1 -> T2) (g : T2 -> T1) (s : seq T2): {in s, cancel g f} -> {in s, pcancel g (fun y => Some (f y))}.
Proof. by move=> can y yin; congr (Some _); apply can. Qed.

Lemma pcan_in_inj (T1 T2 : eqType) (f : T1 -> T2) (g : T2 -> option T1) (s : seq T1) :
  {in s, pcancel f g} -> {in s &, injective f}.
Proof. by move=> fK x y xin yin=> /(congr1 g); rewrite !fK // => [[]]. Qed.

Lemma inj_in_inamp (T1 T2 : eqType) (f : T1 -> T2) (s : seq T1): {in s, injective f} -> {in s &, injective f}.
Proof. by move=> injf x y xin /injf H /eqP; rewrite eq_sym=> /eqP/H ->. Qed.

Lemma can_in_inj (T1 T2 : eqType) (f : T1 -> T2) (g : T2 -> T1) (s : seq T1) : {in s, cancel f g} -> {in s &, injective f}.
Proof. move/can_in_pcan_in. move=> pcan. eapply pcan_in_inj. exact: pcan. Qed.

Lemma mem_in_map (T1 T2 : eqType) (f : T1 -> T2) (s : seq T1) : {in s, injective f} -> forall (x : T1), (f x \in [seq f i | i <- s]) = (x \in s).
Proof. move=> H x. apply /mapP/idP; last by exists x.
                                             by move=> [y yin] /eqP; rewrite eq_sym=> /eqP/H <-.
Qed.

Lemma map_nil_is_nil (A C: eqType) (f : A -> C) (s : seq A) : (map f s == [::]) = (s == [::]).
Proof. by case s. Qed.

Lemma mem_eq_is_nil (A : eqType) (s : seq A) : s =i [::] -> s = [::].
Proof. case: s=> [// | h t] H.
       + have: h \in [::]. rewrite -H in_cons eqxx //.
         by rewrite in_nil.
Qed.

Lemma perm_eq_nil_cons (T: eqType) t (s : seq T) : perm_eq [::] (t :: s) = false.
Proof. by rewrite /perm_eq /= eqxx. Qed.

Lemma mem_cons (T : eqType) (x y : T) (s : seq T) : x \in s -> x \in y :: s.
Proof. by rewrite in_cons orbC=> ->. Qed.

Lemma mem_map_undup (T1 T2 : eqType) (f : T1 -> T2) (s : seq T1):
  map f s =i map f (undup s).
Proof. elim: s=> [//| h t IHl].
       case e: (h \in t)=> x /=.
       have ->: (x \in f h :: [seq f i | i <- t]) = (x \in [seq f i | i <- t]).
       by rewrite -mem_undup /= ; rewrite map_f // mem_undup.
       by rewrite e -IHl.
       by rewrite e !in_cons -IHl.
Qed.

Lemma neq_funapp (T1 T2: eqType) (f: T1 -> T2) : forall t t', f t != f t' -> t != t'.
Proof. by move=> t t'; apply contraPneq=> ->; rewrite eqxx. Qed.

Lemma neq_funapp_inj (T1 T2: eqType) (f: T1 -> T2) : injective f -> forall t t', t != t' -> f t != f t'.
Proof. by move=> inj_f t t'; apply contraPneq=> /inj_f ->; rewrite eqxx. Qed.

Lemma neq_funapp_inj_in (T1 T2: eqType) (f: T1 -> T2) (s : seq T1) :
  {in s&, injective f} -> forall t t', t \in s -> t' \in s -> t != t' -> f t != f t'.
Proof. by move=> inj_f t t' tin t'in; apply contraPneq=> /inj_f -> //; rewrite eqxx. Qed.

Lemma pmapP (T1 T2 : eqType) (f : T1 -> option T2) (s : seq T1) (y : T2):
  reflect (exists2 x : T1, x \in s & Some y = f x) (y \in pmap f s).
Proof. by rewrite mem_pmap; apply mapP. Qed.

Lemma sub_pmap (T1 T2: eqType) (s1 s2: seq T1) (f: T1 -> option T2) :
  {subset s1 <= s2} -> {subset pmap f s1 <= pmap f s2}.
Proof. by move=> sub_s y /pmapP [x x_s]; rewrite mem_pmap=> ->; rewrite map_f ?sub_s. Qed.

Lemma eq_mem_pmap (T1 T2 : eqType) (f : T1 -> option T2) (s1 s2 : seq T1):
  s1 =i s2 -> pmap f s1 =i pmap f s2.
Proof. by move=> Es x; apply /idP/idP; apply sub_pmap=> ?; rewrite Es. Qed.

Lemma uniq_neq_nth (T: eqType) (s : seq T) n m x0 x1:
  uniq s -> n < (size s) -> m < (size s) -> n != m -> nth x0 s n != nth x1 s m.
Proof. move/uniqP=> /(_ x1) us nin min.
       by rewrite (set_nth_default x1) //; apply contra_neq=> /us ->.
Qed.

Lemma in_map_injPn (T1 : eqType) (T2 : eqType) (s : seq T1) (f : T1 -> T2) (us: uniq s):
  reflect (exists2 x, x \in s & exists2 y, (y != x) && (y \in s) & f x = f y) (negb (uniq (map f s))).
Proof.
  apply: (iffP idP) => [injf | [x Dx [y Dxy eqfxy]]]; last first.
  move: (rot_to Dx)=>[i E defE].
  rewrite /dinjectiveb -(rot_uniq i) -map_rot defE /=; apply/nandP; left.
  rewrite eqfxy.
  rewrite map_f //.
  suffices : y \in x :: E.
  rewrite in_cons. case/orP=> //. by move: Dxy; rewrite /negb; case (y == x).
  by rewrite -defE mem_rot; move: Dxy=> /andP[_ ->].
  wlog s1 :/ T1.
  by case: s us injf=> // hd tl us injf hwlog; apply hwlog.
  move : injf=> /uniqPn p.
  have s0 := f s1.
  have [i [j [ijle [jsize [ntheq]]]]]:= p s0.
  exists (nth s1 s i). apply mem_nth. rewrite size_map in jsize.
  apply: ltn_trans ijle jsize.
  exists (nth s1 s j). apply /andP; split.
  apply uniq_neq_nth=> //.
  by rewrite size_map in jsize.
  by rewrite size_map in jsize; apply: ltn_trans ijle jsize.
  rewrite Order.NatOrder.ltn_def in ijle.
  by move/andP : ijle=> [].
  by apply mem_nth; rewrite size_map in jsize.
  erewrite <- nth_map.
  suffices <-: nth s0 [seq f i | i <- s] j = f (nth s1 s j).
  by apply ntheq.
  by apply nth_map; rewrite size_map in jsize.
  by rewrite size_map in jsize; apply: ltn_trans ijle jsize.
Qed.

HB.howto finType.
HB.about isFinite.Build.


(* Lemma in_map_injP (T1 : eqType) (T2 : eqType) (s : seq T1) (f : T1 -> T2) (us: uniq s): *)
Lemma in_map_injP' (T1 : choiceType) (T2 : choiceType) (s : seq T1) (f : T1 -> T2) (us: uniq s):
  reflect {in s&, injective f} (uniq (map f s)).
Proof.
  pose T : finType := (seq_sub s).
  pose g := finfun (fun (x:T) =>  f (\val x)).
  have eq_fg: forall (t:T1) (tin:(t \in s)), f t = (g (@Sub T1 _ T t tin)).
    by move=> t tin; rewrite ffunE SubK.
  have eq_fg': forall (t:T), f (\val t) = (g t).
    by move=> t; rewrite ffunE.
  have eq_uniq: uniq [seq f i | i <- s] = uniq [seq g i | i <- (enum T)].
    have /eq_map eq_map_fg : g =1 (f \o \val) by move=> y /=; rewrite eq_fg'.
    apply eq_uniq; first by rewrite !size_map -enumT -cardE card_seq_sub.
    rewrite eq_map_fg (map_comp f \val).
    apply eq_mem_map.
    by rewrite -codomE; move=> x; rewrite codom_val.
  (* Unset Printing Notations. *)
  have eq_injective : {in s&, injective f} <-> injective g.
  split=> [inj_f|inj_g].
    + move=> [x xin] [y yin].
      rewrite -!eq_fg'=> /(inj_f _ _ (ssvalP _) (ssvalP _)) /= eq_xy.
      by apply val_inj.
    + move=> x y xin yin; rewrite !eq_fg=> eq_gxy.
      suffices [->]: (@Sub _ _ T x xin) = (Sub y yin) by [].
      by apply inj_g in eq_gxy.
  rewrite eq_uniq; apply (iffP idP).
  + move=> ufs t t' tin t'in.
    rewrite !eq_fg=> eq_gxy.
    suffices : (@Sub _ _ T t tin) = (Sub t' t'in) by move=> [->].
    move: eq_gxy.
    by move:ufs=> /injectiveP inj_g /inj_g ->.
  + by move=> /eq_injective/injectiveP //.
 Qed.

Lemma in_map_injP (T1 : eqType) (T2 : eqType) (s : seq T1) (f : T1 -> T2) (us: uniq s):
  reflect {in s&, injective f} (uniq (map f s)).
Proof.
  rewrite -[uniq (map f s)]negbK.
       case: in_map_injPn=> // [noinjf | injf]; constructor.
         case: noinjf => x Dx [y /andP[neqxy /= Dy] eqfxy] injf.
         by case/eqP: neqxy; apply: injf.
       move=> x y Dx Dy /= eqfxy; apply/eqP; apply/idPn=> nxy; case: injf.
       by exists x => //; exists y => //=; rewrite /= eq_sym nxy.
Qed.

Lemma zip_uniq_proj (T1 T2 : eqType) (s1 : seq T1) (s2 : seq T2) :
  (uniq s1) ->
  (size s1 = size s2) ->
  forall (x y: T1 * T2), x \in zip s1 s2 ->
               y \in zip s1 s2 ->
                     x.1 = y.1 -> x = y.
  Proof. move=> us1 eqsize /= [x1 x2] [y1 y2].
         wlog p0 :/ ((T1 * T2)%type).
         by move=> hwlog; apply: hwlog (x1,x2).
         case: p0=> x0 y0.
         suffices minnrefl : minn (size s1) (size s1) = size s1.
           move=> /nthP /= /(_ (x0,y0)) [xn]; rewrite size_zip -eqsize minnrefl=> sizes1.
           rewrite nth_zip // => /eqP; rewrite xpair_eqE=> /andP[/eqP x1nth /eqP x2nth].
           move=> /nthP /= /(_ (x0,y0)) [yn]; rewrite size_zip -eqsize minnrefl=> sizes2.
           rewrite nth_zip // => /eqP; rewrite xpair_eqE=> /andP[/eqP y1nth /eqP y2nth].
           rewrite -x1nth{x1nth} -x2nth{x2nth} -y1nth{y1nth} -y2nth{y2nth}.
           by move=> /eqP; rewrite nth_uniq // => /eqP ->; apply/eqP; rewrite eqxx.
           by rewrite /minn; case e: (_ < _)%N.
  Qed.

  Lemma zip_uniq_proj2 (T1 T2 : eqType) (s1 : seq T1) (s2 : seq T2) :
    (uniq s2) ->
    (size s1 = size s2) ->
    forall (x y: T1 * T2), x \in zip s1 s2 ->
                      y \in zip s1 s2 ->
                            x.2 = y.2 -> x = y.
  Proof. move=> us1 eqsize /= [x1 x2] [y1 y2].
         wlog p0 :/ ((T1 * T2)%type).
         by move=> hwlog; apply: hwlog (x1,x2).
         case: p0=> x0 y0.
         suffices minnrefl : minn (size s1) (size s1) = size s1.
         move=> /nthP /= /(_ (x0,y0)) [xn]; rewrite size_zip -eqsize minnrefl eqsize => sizes1.
           rewrite nth_zip // => /eqP; rewrite xpair_eqE=> /andP[/eqP x1nth /eqP x2nth].
           move=> /nthP /= /(_ (x0,y0)) [yn]; rewrite size_zip -eqsize minnrefl eqsize => sizes2.
           rewrite nth_zip // => /eqP; rewrite xpair_eqE=> /andP[/eqP y1nth /eqP y2nth].
           rewrite -x1nth{x1nth} -x2nth{x2nth} -y1nth{y1nth} -y2nth{y2nth}.
           by move=> /eqP; rewrite nth_uniq // => /eqP ->; apply/eqP; rewrite eqxx.
         by rewrite /minn; case e: (_ < _)%N.
  Qed.

  Lemma in_zip_l (S T: eqType) (s1 : seq S) (s2 : seq T) (s0 : S) :
    (size s1 = size s2) ->
    s0 \in s1 ->
    exists st, st \in zip s1 s2 /\ st.1 = s0 /\ exists t, st.2 = t.
  Proof. move=> eqsize sin /=.
         wlog t0 :/ (S * T)%type.
           move=> hwlog. apply hwlog.
           case :s2 eqsize sin {hwlog}; first by move=> /= /size_0_nil ->; rewrite in_nil.
           by move=> a _ _ _ ; exact (s0,a).
         case t0=> sd td; exists (nth (sd,td) (zip s1 s2) (index s0 s1)).
         split.
         + by rewrite mem_nth // size_zip -eqsize /minn; case e: (size s1 < size s1)%N; rewrite index_mem //.
         + split; first by rewrite nth_zip //= nth_index //.
         + by exists (nth td s2 (index s0 s1)); rewrite nth_zip.
  Qed.

  Lemma sort_cons (T : Type) (leT : rel T) : total leT -> transitive leT ->
    forall (s1 s2 : seq T) (x : T),
    sort leT s1 = x :: s2 -> s2 = sort leT s2.
  Proof. move=> tot trans s1 s2 x eq.
    suffices /sorted_sort : sorted leT s2.
      by move=> /(_ trans) ->.
    have:= sort_sorted tot s1.
    by rewrite eq -cat1s=> /cat_sorted2 [_ ->].
  Qed.

  Lemma sort_nil (T : eqType) (leT : rel T) :
    total leT -> transitive leT -> antisymmetric leT ->
    forall (s1 : seq T),
    sort leT s1 = [::] -> s1 = [::].
  Proof.
    move=> tot trans anti s1; suffices nil_sorted: [::] = sort leT [::].
      by rewrite nil_sorted=> /(perm_sortP tot trans anti)/perm_nilP ->.
    by [].
  Qed.

  Lemma index_map_in [T1 T2 : eqType] [f : T1 -> T2] (s : seq T1) :
    {in s&, injective f} -> forall (x : T1), x \in s -> index (f x) [seq f i | i <- s] = index x s.
  Proof. elim: s => [//| a t IHtl] inj_f x xin /=.
         case : ifP; first by move/eqP/inj_f; rewrite in_cons eqxx /==> /(_ isT) -> //; rewrite eqxx.
         case : ifP; first by move/eqP=> ->; rewrite eqxx //.
         move=> neq fneq; rewrite IHtl //.
         by move=> ? ? xinn yinn; apply inj_f; rewrite in_cons ?xinn ?yinn orbT.
         by move: xin; rewrite in_cons eq_sym neq /=.
  Qed.

  Lemma size_filter_le {T : Type} (f : T -> bool) (l : seq T) : size (filter f l ) <= size l.
  Proof.
  elim: l=> // hd tl IHl /=.
  by case: ifP=> [//| _]; apply (leq_trans IHl).
  Qed.

  Lemma map_snd_zip_size (T U: Type) (s1 : seq T) (s2 : seq U) :
    size s1 = size s2 -> map snd (zip s1 s2) = s2.
  Proof.
  elim: s2 s1=> [//|hd tl IHtl] [//|// a tla] eq_size /=.
  by congr cons; rewrite IHtl //; move: eq_size=> /=[->].
  Qed.

  Lemma map_fst_zip_size (T U: Type) (s1 : seq T) (s2 : seq U) :
    size s1 = size s2 -> map fst (zip s1 s2) = s1.
  Proof.
  elim: s2 s1=> [//|hd tl IHtl] [//|// a tla] eq_size /=.
  by congr cons; rewrite IHtl //; move: eq_size=> /=[->].
  Qed.

  Lemma zip_proj1 (T S : Type) (s1 : seq T) (s2 : seq S) :
    size s1 = size s2 ->
    map fst (zip s1 s2) = s1.
  Proof.
  elim: s1 s2=> [| hd tl IHtl] s2; first by case: s2.
  case: s2=> [//|hd2 tl2] /=[]eq_size.
  by congr cons; apply IHtl.
  Qed.

  Lemma count_mem_inP (T : eqType) (t : T) (s :seq T):
    reflect (count_mem t s >= 1) (t \in s).
  Proof.
  apply (iffP idP).
  + rewrite -mem_undup=> t_in.
    suffices uniq_count: 1 <= count_mem t (undup s).
      by apply (leq_trans uniq_count (count_undup _ _)).
    move: t_in (@count_uniq_mem _ (undup s) t (undup_uniq s)).
    by move=> -> ->.
  + elim: s=> [//|hd tl IHtl].
    rewrite /= in_cons eq_sym.
    case: (_ == _)=> [//|/=].
    by rewrite add0n; apply IHtl.
  Qed.

  Corollary count_mem_1_in (T : eqType) (t : T) (s : seq T):
    count_mem t s = 1 -> t \in s.
  Proof. by move=> eq; apply /count_mem_inP; rewrite eq. Qed.

  Lemma neq_SSnm (n m: nat):
    n.+1 != m.+1 -> n != m.
  Proof.
  rewrite /negb.
  case_eq (n == m)=> [|//].
  by move=> /eqP ->; rewrite eqxx.
  Qed.

  Lemma neq_count_mem {T : eqType} {t1 t2 : T} (s : seq T):
    count_mem t1 s != count_mem t2 s -> t1 != t2.
  Proof.
  elim:s=> [//| hd tl IHtl] /=.
  case_eq (hd == t1); case_eq (hd == t2).
  + move=> _ _ /=.
    by rewrite !add1n=> /neq_SSnm/IHtl->.
  + by move=> concl /eqP <-; rewrite concl.
  + by move /eqP ->; rewrite eq_sym=> ->.
  + by move=> _ _ /=; rewrite add0n=> /IHtl.
  Qed.


  Lemma count_filter (T : Type) (a : pred T) (s : seq T):
    count a s = count a (filter a s).
  Proof.
  elim s=> [//|hd tl IHtl] /=.
  by case_eq (a hd)=> /= [->|]; rewrite IHtl.
  Qed.

  Lemma le_count_rem (T : eqType) (x : T) (P : pred T) (s : seq T):
    count P s <= count P (rem x s) + (x \in s) && P x.
  Proof.
  rewrite count_rem addnBC.
  set x' := (_ && _); set n:= count P s.
  by case: x'; case: n=> //.
  Qed.

  Lemma nat_coq_nat (n m : nat) :  (n < m)%nat = (n < m). Proof. by []. Qed.
  Lemma nat_coq_le_nat (n m : nat) :  (n <= m)%N = (n <= m). Proof. by []. Qed.

  Lemma lt_count_subpred (T : eqType) (p1 p2 : pred T) (s : seq T):
    subpred p1 p2 -> (exists (x:T), [&& p2 x, ~~ (p1 x) & x \in s]) ->
    count p1 s < count p2 s.
  Proof.
  move=> spred [x /and3P[p2x /negPf p1xPn]].
  elim s=> [//|hd tl IHtl].
  rewrite in_cons=> /orP[/eqP <-|/IHtl count_tl] /=.
  + rewrite p2x p1xPn /= add0n add1n.
    by apply (leq_ltn_trans (sub_count spred _) (ltnSn _)).
  case_eq (p1 hd)=> [/spred -> | _] /=.
  + by rewrite !add1n -nat_coq_nat /= ltnS nat_coq_nat count_tl.
  case: (p2 hd); rewrite !add0n ?add1n; last by rewrite count_tl.
  by apply (ltn_trans count_tl (ltnSn _)).
  Qed.

  Lemma count_mem_filter (n m : nat) (s: seq nat):
    n != m -> count_mem m s = count_mem m (filter (predC1 n) s).
  Proof.
  elim: s=> [//|hd tl IHtl] /=.
  case: ifP.
  + rewrite /==> /negPf neq_hdn /negPf neq_hdm.
    by congr addn; apply IHtl; apply /negPf.
  rewrite {1}/negb.
  case: ifP=> [/eqP -> _|//] neq_nm.
  by rewrite (negPf neq_nm) (IHtl neq_nm).
  Qed.

  Lemma allPn_count (T : Type) (a : pred T) (s : seq T): all (predC a) s = (count a s == 0).
  Proof.
  apply /idP/idP; elim: s=> [//| hd tl IHtl] /=.
  + by move=> /andP[/negPf -> /IHtl ->].
  case_eq (a hd); first by rewrite add1n //.
  by move=> _ /= /IHtl ->.
  Qed.



  Lemma eq_map_zip_nseq : forall (T U : Type) (s : seq T) (u : U),
    map (fun t=> (t,u)) s = zip s (nseq (size s) u).
  Proof.
  move=> T U s u.
  by elim: s=> [//|hd tl IHtl] /=; rewrite IHtl.
  Qed.

  Lemma perm_eq_zip_eql (T U : eqType) (s1 s2 : seq T) (s3 : seq U):
    size s1 = size s2 ->
    size s1 = size s3 ->
    constant s3 ->
    perm_eq s1 s2 ->
    perm_eq (zip s1 s3) (zip s2 s3).
  Proof.
  case: s2; case: s3; case: s1=> //.
  move=> a1 tl1 a3 tl3 a2 tl2 /= [eq_size12] [eq_size13] const_s.
  move=> /(perm_map (fun t=> (t,a3))); rewrite !eq_map_zip_nseq /=.
  suffices tl3_nseq : (nseq (size tl1) a3) = tl3.
    by rewrite -eq_size12 !tl3_nseq.
  rewrite eq_size13.
  elim: tl3 const_s {eq_size13}=> [//|hd tl IHtl] /=.
  by move=> /andP[/eqP -> eq_tl]; rewrite IHtl //.
  Qed.


Lemma size_proj (T1 T2 : Type) (s : seq (T1 * T2)) :
  size [seq i.2 | i <- s] = size [seq i.1 | i <- s].
Proof. by elim: s=> [//| h tl IHtl] /=; rewrite IHtl. Qed.

Lemma pair_zip_in (T1 T2 : eqType) (s1 : seq T1) (s2 : seq T2) (x0 x : T2) (y1: T1) (i : nat) :
  size s1 = size s2 -> (i < size s2)%N -> nth x0 s2 i = x ->
  exists t1, (t1,x) \in zip s1 s2 /\ nth (t1,x0) (zip s1 s2) i = (t1,x).
Proof.
move=> eq_size i_in <-.
exists (nth y1 s1 i); split.
+ rewrite -nth_zip //; apply mem_nth.
  by rewrite size_zip eq_size ltn_min i_in.
+ rewrite nth_zip //; congr pair.
  by apply set_nth_default; rewrite eq_size.
Qed.